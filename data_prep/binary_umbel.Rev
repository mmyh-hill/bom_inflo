morpho <- readDiscreteCharacterData("data/umbel.nexus")

phylogeny <- readTrees("data/bom_only_MAP.tre")[1]

moves = VectorMoves()
monitors = VectorMonitors()

rate_pr := phylogeny.treeLength() / 10
mu ~ dnExp(rate_pr)
moves.append( mvScale(mu, lambda=1, weight=2.0) )

NUM_STATES = 2

NUM_RATES = NUM_STATES * (NUM_STATES-1)
for ( i in 1:NUM_RATES ) {
    rate[i] := mu
}

Q_morpho := fnFreeK( rate, rescale=false )
phyMorpho ~ dnPhyloCTMC(tree=phylogeny, Q=Q_morpho, type="Standard")
phyMorpho.clamp(morpho)
mymodel = model(phylogeny)
monitors.append( mnModel(filename="output/umbel.log", printgen=10) )
monitors.append( mnScreen(printgen=100) )
monitors.append( mnJointConditionalAncestralState(tree=phylogeny,
                                                   ctmc=phyMorpho,
                                                   filename="output/umbel.states.txt",
                                                   type="Standard",
                                                   printgen=1,
                                                   withTips=true,
                                                   withStartStates=false) )

mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")
mymcmc.run(generations=25000, tuningInterval=200)

anc_states = readAncestralStateTrace("output/umbel.states.txt")
anc_tree = ancestralStateTree(tree=phylogeny, ancestral_state_trace_vector=anc_states, include_start_states=false, file="output/umbel_ase_ERM.tree", burnin=0.25, summary_statistic="MAP", site=1)

